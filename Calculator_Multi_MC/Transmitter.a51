ORG 00

	LCD_PORT  		 EQU  P2
	RS_PIN    		 EQU  P3.4
	EN_PIN  	     EQU  P3.5
	
	KEY_PAD_PORT     EQU  P1
	ROW_0            EQU  P1.0
	ROW_1            EQU  P1.1
	ROW_2            EQU  P1.2
	ROW_3            EQU  P1.3
	COLM_0           EQU  P1.4
	COLM_1           EQU  P1.5
	COLM_2           EQU  P1.6
	COLM_3           EQU  P1.7	
		
	OPERAND_1        EQU  R5
	OPERATOR         EQU  R6
    OPERAND_2        EQU  R7
	
MAIN:

    ;INITIATE LCD AND KEYPAD
	LCALL LCD_INIT
	LCALL KEY_PAD_INIT
	LCALL SERIAL_INITIALIZATION
	MOV DPTR,#MSG2
	
	MOV A,#0X83              ;SET CURSOR TO ROW 1, FORTH COLUMN
	LCALL LCD_CMD 
	
AGAIN:	

	LCALL KEY_PAD_SCAN
	CJNE  A,#'C',SKIP1
	LJMP MAIN
	SKIP1:
	LCALL LCD_CHAR
	LCALL DELAY_HS			 ;TO MAKE SURE THAT THE USER PULL HIS FINGER
	CLR C
	SUBB A,#0X30             ;A=A-0X30-C
	MOV OPERAND_1,A
	
	
	LCALL KEY_PAD_SCAN
	CJNE  A,#'C', SKIP2
	LJMP MAIN
	SKIP2:
	LCALL LCD_CHAR
	LCALL DELAY_HS
	MOV OPERATOR,A
	
	LCALL KEY_PAD_SCAN
	CJNE  A,#'C', SKIP3
	LJMP MAIN
	SKIP3:
	LCALL LCD_CHAR
	LCALL DELAY_HS
	CLR C
	SUBB A,#0X30	
	MOV OPERAND_2,A
	
	LOOP:
	LCALL KEY_PAD_SCAN
    CJNE  A,#'C', SKIP4
	LJMP MAIN
	SKIP4:
	CJNE  A,#'=',LOOP
	
	MOV A,#0XC9              ;SET CURSOR TO ROW 2, COLUMN 10
	LCALL LCD_CMD 
	
	SEND_AGAIN:
	;SEND DATA TO THE OTHER MICROCONTROLLER 
	MOV A,OPERAND_1
	ACALL SERIAL_SEND_BYTE
	MOV A,OPERAND_2
	ACALL SERIAL_SEND_BYTE
	MOV A,OPERATOR
	ACALL SERIAL_SEND_BYTE

	
	
	;RECEIVE THE TRANSMITTED DATA AGAIN
	ACALL SERIAL_RECEIVE_BYTE
	PUSH ACC
	ACALL SERIAL_RECEIVE_BYTE
	PUSH ACC
	ACALL SERIAL_RECEIVE_BYTE
	PUSH ACC
	
	;CHECK EACH BYTE
	POP 03H
	MOV A,OPERATOR
	CLR C
	SUBB A,R3
	JNZ SEND_AGAIN
	
	POP 03H
	MOV A,OPERAND_2
	CLR C
	SUBB A,R3
	JNZ SEND_AGAIN
	
	POP 03H
	MOV A,OPERAND_1
	CLR C
	SUBB A,R3
	JNZ SEND_AGAIN

	
    ACALL SERIAL_RECEIVE_BYTE
    ACALL LCD_CHAR
   
	ACALL SERIAL_RECEIVE_BYTE
    ACALL LCD_CHAR

	ACALL SERIAL_RECEIVE_BYTE
    ACALL LCD_CHAR

	ACALL SERIAL_RECEIVE_BYTE
    ACALL LCD_CHAR
	
	LJMP AGAIN
	;---------------LCD_SUBROUTINES---------------
	LCD_INIT:
				MOV A,#0X38     ;2-line/5*7
				LCALL LCD_CMD
				LCALL DELAY_MS
				MOV A,#0X0C     ;display on,cursor off
				LCALL LCD_CMD
				LCALL DELAY_MS
				MOV A,#0X06     ;shift cursor to right
				LCALL LCD_CMD
				LCALL DELAY_MS
				MOV A,#0X01     ;clear display screen 
				LCALL LCD_CMD
				LCALL DELAY_MS
				RET
				
	LCD_CMD:
			CLR RS_PIN
			SETB EN_PIN 
			lCALL  DELAY_MS
			MOV LCD_PORT,A
			CLR  EN_PIN
			RET
	LCD_CHAR:
			SETB RS_PIN
			SETB EN_PIN
			LCALL  DELAY_MS
			MOV LCD_PORT,A
			CLR  EN_PIN
			RET
			
	LCD_STRING:
			MOV A,#0
			MOVC A,@A+DPTR
			CJNE  A,#0,PRINT
			SJMP STRING_COMPLET
				PRINT:
					LCALL LCD_CHAR
					INC DPTR
					SJMP LCD_STRING
			STRING_COMPLET:
					RET
	;---------------KEY_PAD_SUBROUTINES---------------
	KEY_PAD_INIT:
			MOV KEY_PAD_PORT,#0XF0    ;initiate the rows as output & columns as input
			RET
	KEY_PAD_SCAN:
	        MOV KEY_PAD_PORT,#0XF0    ;INITAITE THE PORT VALUE
			
			MOV A,KEY_PAD_PORT        ; READ THE PORT VALUE
			
			CJNE A,#0XF0,GET_KEY      ;IF THE USER PRESS DOWN ANY BUTTON JUMP TO GET KEY SUBROUTINE
			SJMP KEY_PAD_SCAN         
				GET_KEY:
					MOV KEY_PAD_PORT,#0XFE  ;CLR THE FIRST ROW
					MOV R1,#0
					JNB COLM_0,NO_0
					JNB COLM_1,NO_1
					JNB COLM_2,NO_2
					JNB COLM_3,NO_3
					MOV KEY_PAD_PORT,#0XFD  ;CLR THE FIRST ROW
					MOV R1,#4
					JNB COLM_0,NO_0
					JNB COLM_1,NO_1
					JNB COLM_2,NO_2
					JNB COLM_3,NO_3
					MOV KEY_PAD_PORT,#0XFB  ;CLR THE FIRST ROW
					MOV R1,#8
					JNB COLM_0,NO_0
					JNB COLM_1,NO_1
					JNB COLM_2,NO_2
					JNB COLM_3,NO_3
					MOV KEY_PAD_PORT,#0XF7  ;CLR THE FIRST ROW
					MOV R1,#12
					JNB COLM_0,NO_0
					JNB COLM_1,NO_1
					JNB COLM_2,NO_2
					JNB COLM_3,NO_3
					SJMP KEY_PAD_FINISH
					     NO_0:
							MOV A,R1
							MOVC A,@A+DPTR
							SJMP KEY_PAD_FINISH
						 NO_1:
							MOV A,R1
							ADD A,#1
							MOVC A,@A+DPTR
							SJMP KEY_PAD_FINISH
						 NO_2:
						 MOV A,R1
							ADD A,#2
							MOVC A,@A+DPTR
							SJMP KEY_PAD_FINISH
						 NO_3:
						 MOV A,R1
							ADD A,#3
							MOVC A,@A+DPTR
							SJMP KEY_PAD_FINISH
			KEY_PAD_FINISH:
					RET
	;---------------DELAYS_SUBROUTINES---------------				
	DELAY_MS:
			MOV R0,#200
			LOOP_2:
				MOV R1,#200
			LOOP_1:
				DJNZ R1,LOOP_1
		        DJNZ R0,LOOP_2
				RET
	DELAY_HS:
			MOV R0,#10
			LOOP__3: MOV R1,#100
			LOOP__2: MOV R2,#200
			LOOP__1: DJNZ R2,LOOP__1
					 DJNZ R1,LOOP__2
					 DJNZ R0,LOOP__3
			RET
	;-----------------SERIAL_SUBROUTENS--------------
	SERIAL_INITIALIZATION:
	
		MOV TMOD,#0X20
		MOV TH1,#-3   ;9600 baud rate
		MOV SCON,#0X50
		SETB TR1
		SETB REN
	RET
	
	SERIAL_SEND_BYTE:
		MOV SBUF,A
		TRANSMIT_LOOP:
		JNB TI,TRANSMIT_LOOP
		CLR TI
	RET
	
	SERIAL_RECEIVE_BYTE:
		RECEIVE_LOOP:
		JNB RI,RECEIVE_LOOP
		MOV A,SBUF
		CLR RI
	RET

	;---------------MEMORY-ASSIGNMENT---------------
	ORG 300H
		MSG2:	DB '1','2','3','+','4','5','6','-','7','8','9','*','C','0','=','/'
	FINISH:			
			END